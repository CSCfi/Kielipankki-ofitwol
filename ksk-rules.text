Co = [PI .o. [b|d|f|g|h|k|l|m|n|p|r|s|t|v]] | j ;
Ci = [[Co].u .o. PI] ;
CØ = [Ci - Co] ;
Vo = PI .o. [a | e | i | o | u | y | ä | ö] ;
Vok = [Vo - {ij}:] ;
Vi = [[Vo].u .o. PI] ;
VØ = [Vi - Vo] ;
VV = [:a  :Ø* :a|:e :Ø* :e|:i :Ø* :i|:o :Ø* :o|:u :Ø* :u|:y :Ø* :y|:ä :Ø* :ä|:ö :Ø* :ö] ;
VVØ = [:a :Ø* [:a & :Ø]] | [:e  :Ø* [:e & :Ø]] | [:i  :Ø* [:i & :Ø]] | [:o  :Ø* [:o & :Ø]] | [:u :Ø* [:u & :Ø]] | [:y :Ø* [:y & :Ø]] | [:ä :Ø* [:ä & :Ø]] | [:ö :Ø* [:ö & :Ø]] ;
VhV = [:a :h (:Ø) :a|:e :h (:Ø) :e|:i :h (:Ø) :i|:o :h (:Ø) :o|:u :h (:Ø) :u|:y :h (:Ø) :y|:ä :h (:Ø) :ä|:ö :h (:Ø) :ö] ;
VbV = [:e :a|:e :ä|:i :a|:i :o|:i :ö|:o :a|:u :a|:u (:Ø) :e|:y (:Ø) :e] ;
Sy = [Vo+ [Co|CØ]+] ;
!CLOSED = Vi ({ij}:i) Ci (VØ) [Ci | END] ; 
CLOSED = Vi ({ij}:i|{ØiØ}:) Ci (VØ) [Ci | END] ;
OPEN = Vo ((VØ) {ij}:i|{ØiØ}:) [Ci Vo|END] ;


!!STOP

{V}:a <=> :a :Ø* ({Øh}:) _ ; ! maah<a>n
{V}:e <=> :e :Ø* ({Øh}:) _ ; ! nalle<e>n
{V}:i <=> :i :Ø* ({Øh}:) _ ; ! risti<i>n
{V}:o <=> :o :Ø* ({Øh}:) _ ; ! valo<o>n
{V}:u <=> :u :Ø* ({Øh}:) _ ; ! parru<u>n
{V}:y <=> :y :Ø* ({Øh}:) _ ; ! kyky<y>n
{V}:ä <=> :ä :Ø* ({Øh}:) _ ; ! pääh<ä>n
{V}:ö <=> :ö :Ø* ({Øh}:) _ ; ! yöh<ö>n

{aØ}:Ø <=> _ [{ij}: | t] ; ! ma<a> ma<>issa, koir<>ia, jumal<>ten
{ao}:o <=> _ {ij}: ; ! kair<a> kair<o>ja 
{aØo}:o => _ [{ij}:j | {ij}:i :Ø* Co] ; ! perun<o>ita
{aØo}:Ø => _ {ij}:i ; ! perun<>ia ** toghether with GEN & PTV 
{aØo}:a /<= _ {ij}: ; ! perun<a>
{ØaØa}:Ø => _ [{ij}: | :t |END] ; ! koiras<> koira<a>n koiras<>ta
{ØaØa}:a /<= _ [{ij}:|END] , :s _ ;
        ! koira<>issa koiras<>ta vasen<> vasemp<a>ta

{ØaØØ}:Ø|{ØäØØ}:Ø => [:h|:n|:t|{sØh}:|{tØhn}:&Co|:n {ØpmØ}:Ø] _ ({nØØ}:|{ØnØ}:) [{Øt}:|:t|{nrs}:] , _ ({nØØ}:Ø|{ØnØ}:) [{ij}:|END] ;
{ØaØØ}:a|{ØäØØ}:ä /<= [:h|:n|:t|{sØh}:|{tØhn}:&Co|:n {ØpmØ}:Ø] _ ({nØØ}:|{ØnØ}:) [{Øt}:|:t] , _ ({nØØ}:Ø|{ØnØ}:Ø) [{ij}:|END] ;
        ! pahin<> pahimm<a>n pahin<>ta (pahin<>na)
	! muudan<> muutam<a>n muuat<>ta (muuan<>na)
	! saapa<>s saappa<a>n saapa<>sta
        ! vieras<> viera<a>n vierah<a>n vieras<>ta (vieras<>na)
	! lämmin<> lämpim<ä>n lämmin<>tä (lämmin<>nä) lämpim<>iä
	! kevät<> kevä<ä>n kevät<>tä (kevän<>nä) kevä<>itä

{aä}:a <=> [:a|:o|:u] :* _ ; ! maass<a>, piiss<ä>

{ØeØeØ}:e /<= _ ({nØØ}:Ø) [{Øt}:|:t|{ij}:|END] ;
{ØeØeØ}:Ø =>  _ ({nØØ}:Ø) [{Øt}:|:t|{ij}:|END] , Vo (CØ) [:s|:n] _ ({nØØ}:) {nrs}: ;
        ! ahven<> ahven<e>n ahven<>ta ahven<e>na ahven<>na
        ! hevos<>ten, hevos<>ien, hevos<>na

{ØeØe}:Ø <=> _ [{ij}: | {Øt}: | :t | END] ;
        ! askel<> askel<e>n askel<>ta askel<e>n
{eØ}:Ø <=> _ {ij}: , :h (:Ø) Vo s e _ ;
        ! leve<e>~leve<>itä kaunihise<>n
        ! vaatteese<e>n vaattehese<>n vaattehisi<>n 

{Øh}:Ø => Co Vo ({nØØ}:Ø) _ , VbV _ , Co VØ ({nØØ}:Ø) {ij}:i _ ;
        ! talo<>on valtio<>on hevose<>en hevosi<>in

{ØhØ}:h => o _ [:o [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ,  a _ [:a [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ,  e _ [:e [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ,  a _ CØ [:a [:n|:s]|VØ {ij}: [:s|:t|Vo Co|{Øt}:Ø :a]] ;
        ! vapa<>a vapa<h>an vapa<h>isin
	! tieno<>o tieno<h>on tieno<h>isin
	! korke<>e tieno<>o koira<h>an
	! NOTE: some overgenerated forms remain

{hs}:s => _ VØ [{nrs}:|{Øt}:|:t|END] ;
{hs}:h /<= _ VØ [{Øt}:|:t|END] ;
        ! mie<s> mie<h>en mie<s>tä mie<h>enä mie<h>iä

{Øht}:h|{Øhtn}:h => _ [:e|:i|:u] [:n|:s],_ VØ {ij}:i [e|s|:t|{Øt}: {aä}:];
{Øht}:t <=> _ VØ [{Øt}:|:t] ;
        ! vaatte<h>en vaatte<h>esen vaatte<h>ia vaatte<h>isin
	! ori<h>in ori<h>isen kiiru<h>un kiiru<h>isin
        ! vaate<t>ta ori<t>ta kiiru<t>ta askele<> askele<t>ta

{Øhtn}:n => _ VØ {nrs}: ;
{Øhtn}:t <=> _ VØ [{Øt}:|:t] ;
        ! terve<> (terve<h>en) (terve<n>nä)
	! terve<t>ä (terve<t>ten)

{tØhn}:t | {tØh}:t <=> _ VØ [:t | END] ;
{tØhn}:n => _ VØ {nrs}: ;
{tØhn}:h => _ [Vo-{ij}:] , _ VØ {ij}:i [e|:s|:t|e|{Øt}:Ø {aä}:] ;
{tØhn}:Ø => _ VØ {ij}: , _ Vo ;
        ! kuollu<t> kuollu<t>ta (kuollu<t>ten) kevä<t>
	! (kuollu<n>na) (kevä<n>nä)
	! kevä<h>än kuolle<h>en kevä<h>änä (kevä<h>itten) kuolle<h>isin
        ! kevä<>än kevä<>änä kuolle<>en kuolle<>ena
	! kevä<>itä kevä<>isiin kuolle<>ita kuolle<>itten

{tØh}:h => _ VØ {ij}:i [e|:s|:t|e|{Øt}:Ø {aä}:] ;
        ! airu<h>itten airu<h>ien (airu<h>isin)
	! äity<h>itten äity<h>ien (äity<h>isin)

{iØ}:Ø <=> _ (e) {ij}: , :h Vo s e _ , :h (:Ø) VØ {ij}:i s i _ ; 
        ! t<i>e~t<>eitä, kame<e>~kame<>ita vapaisi<i>in vapahisi<>n

{ØiØ}:i => _ :n VØ [{nrs}: | :t | END ] ;
        ! onneto<i>n onneto<i>nta onneto<i>nna

{ØiØØ}:Ø =>  _ [{ij}: | :t | END] , :s _ {nrs}: ;
{ØiØØ}:i /<=  _ [{ij}: | :t | END] ;
        ! kauris<> kauris<>ta (kauris<>na)
	! kauri<i>na  kaurih<i>n

{ØiØi}:Ø <=> _ [:t | {ij}: | END] ;
        ! ori<> ori<i>n orit<>ta ori<i>na ori<>ita

{ij}:i => Co :Ø* _ , _ :Ø* Co ;
{ij}:j => Vo _ :Ø* Vo ;
        ! talo<i>ssa talo<j>a

{Øiie}:Ø <=> _ END , _ {ij}: e ; ! kalsium<> kalsium<>ien

{iie}:Ø <=> _ {ij}: e ; ! rist<>ien
{iie}:i|{Øiie}:i /<= _ [{ij}:|i] ;
        ! kalsium<e>ja rist<e>jä (rist<e>in) sankar<e>ita (sankar<e>in)
{Øiie}:e|{iie}:e  => _ [{ij}:|i] ;
        ! kalsium<i>a rist<i>ä sankar<i>na
{iiee}:e => _ {sØh}: VØ {ij}: ;
        ! kaun<e>issa (kaun<e>hien)
        ! kaun<i>s kaun<i>in kaun<i>sta kaun<i>ina

{iiØie}:Ø <=> _ [:t|{ij}: e] ; ! (sankar<>ten) sankar<>ien
{iiØie}:e => _ [:i|{ij}:]; ! sankar<e>ita (sankar<e>in)
{iiØie}:i /<= _ [:i|{ij}:]; ! sanka<i> sankar<i>a sankar<i>na

{ieee}:i <=> _ END ;
{ieee}:Ø <=> _ {ij}: ;
        ! tupp<i> tup<e>n tupp<e>a tupp<e>na tupp<>ia

{ieØe}:i <=> _ END ;
{ieØe}:Ø => _ {ij}: , [:h :Ø*|:l|:n|:r|Vo :Ø* :s :Ø*|:t] _ [{Øt}: | :t] ;
{ieØe}:e /<= _ {ij}: ;
{ieØe}:e /<= [:l|:n|:r|Vo :s|:t] _ [{Øt}:|:t] ;
        ! niem<i> käs<i> loh<i>
	! niem<e>n niem<e>ä niem<e>nä käd<e>n kät<e>nä loh<e>n 
	! nien<>tä niem<>iä kät<>tä käs<>iä

{ieØØ}:i <=> _ END ;
{ieØØ}:Ø => _ [{ij}: | :t] , Vo (CØ) [:s|:r|:n] _ {nrs}: ;
{ieØØ}:e /<= _ [{ij}:|:t] , Vo (CØ) Co _ {Øt}: ;
	! pien<i> laps<i> pien<e>nä laps<e>na haps<e>a
        ! pien<>tä pien<>nä pien<>iä las<>ta las<>na
	! nuor<i> nuor<e>n nuor<>ta (nuor<>ra)

{khk}:k <=> _ :s ;
        ! ka<k>si ka<h>den ka<h>ta

{kkØ}:Ø <=> _ s VØ :t ;
        ! su<k>si su<k>sea su<>sta u<k>si u<>sta

{ØkØ}:Ø <=> _ s VØ [:t | END] ;
        ! vastau<>s vastau<k>sen vastau<>sta vastau<k>sena

{ØØk}:k <=> _ :s VØ {ij}: ;
        ! kalleu<>s kalleu<>den kalleu<>tta kalleu<k>sia

{ØkØØ}:Ø <=> _ s VØ [:t | {nrs}: | END]; ! koira<>sta (koira<>sna)
        ! koira<k>sen koira<k>sena koira<k>sia

{kØØ}:Ø | {tØØ}:Ø => _ CLOSED , _ [:o|:u|:y|:ö] {ij}:i [:t|:h] ;
{kØØ}:k | {tØØ}:t /<= _ CLOSED , _ [:o|:ö] {ij}:i :t ;
        ! harak<>an harak<>oissa harak<>oita harak<>oihin
        ! harak<k>oihin harak<k>oja harak<>oita *harak<k>oita
        ! ilmet<t>ty ilmet<t>yjä ilmet<>yitä *ilmet<t>yitä

{kØ}:Ø <=> _ CLOSED ; ! ark<k>u ark<>un

{mnt}:n <=> _ VØ [{nrs}: | END] ;
{mnt}:t <=> _ VØ :t ;
        ! muua<t>ta muua<n> (muua<n>na)

{mn}:m <=> _ :Ø* [Vo | :m | :p] ;
        ! lu<m>en tuo<m>en nie<m>en uisti<m>en
	! onnetto<m>an lämpi<m>än pahi<m>man vase<m>man seitse<m>än

{ØnØ}:Ø <=> _ :t VØ [:t|END], _ :n VØ :t, _ [:n|:s] VØ [{nrs}:|END];
        ! tuha<>t tuha<n>en tuha<>tta tuha<n>tena tuha<n>sia
	! kahdeksa<>s kahdeksa<n>nen kahdeksa<>tta khdeksa<n>tena

{nØØ}:n <=> _ END ; ! hevone<n> hevose<>ssa seitsemä<n> seitsemä<>ssä

{nss}:n <=> _ :e :n ; ! hevo<n>en hevo<s>en hevo<s>ta hevo<s>ia

{nrs}:r => :r VØ _ ; 
{nrs}:s => :s VØ ({nØØ}:Ø) _ ;
        ! nuor<r>a las<s>sa nuore<n>a nuor<n>a las<n>a lapse<n>a

{oØ}:Ø <=> _ {ij}: ;
        ! tieno<o> tieno<o>ta tieno<>ita aino<o>

{ØoØo}:Ø => _ [:t|{ij}:|:i|END] ;
{ØoØo}:o /<= _ [:t|{ij}:|:i|END] ;
        ! uro<>s uro<>sta uro<o>n uro<o>na (uroh<o>na)

{Øpm}:Ø <=> _ VØ [:t | END] ;
{Øpm}:m <=> _ CLOSED ;
        ! vasen<> vasen<>ta vasem<p>ana vasem<p>ia vasem<m>an vasem<m>issa

{ppØ}:p <=> _ s :Ø* Vo ;
        ! la<p>si la<p>sen la<>sta la<p>sia

{ØpmØ}:Ø <=> _ VØ [:t | {nrs}: | END] ;
{ØpmØ}:m <=> _ CLOSED ;
        ! pahin<> pahin<>ta (pahin<>na) pahim<p>ana pahim<p>ia
	! pahim<m>an pahim<m>issa

{pØ}:Ø <=> _ CLOSED ;
        ! tup<p>i tup<>en saap<>as saap<p>aan

{pm}:m <=> _ CLOSED ;
        ! läm<m>in läm<p>imän läm<m>intä

{sØh}:s <=> _ VØ [:t | {nrs}: | END] ;
{sØh}:h => _ Vo [:n|:s] , _ VØ {ij}:i [(:t :t) e n|{Øt}:Ø {aä}:|s] ;
        ! viera<s> viera<s>ta (viera<s>na) uro<s> uro<s>ta uro<s>ten
	! (viera<h>an) (viera<h>ana) (viera<h>isin)

{sØsØ}:s <=> _ Vi [:t | END] ; ! koira<s> koira<s>ta
        ! koira<>an koira<>ana koira<>ina

{Øt}:Ø /<= [{ieØe}:Ø|{ieØØ}:Ø|{ØeØe}:|{ØeØeØ}:|{ØaØØ}:Ø|{ØäØØ}:Ø] ({ØnØ}:Ø|{nØØ}:Ø) _ ;
{Øt}:Ø /<= [VV |VVØ {ij}:| Vo Vo {ij}:] _ ;
        ! *maa<>a maa<t>a *ainoo<>a ainoo<t>a
	! *autioj<>a autioi<t>a
{Øt}:Ø /<= [{uØ}: o|{iØ}: e] ({ij}:) _ ;
	! *suo<>a suo<t>a *soi<>a soi<t>a tei<t>ä
{Øt}:t /<= Co [:o|:u|:y|:ö|{iie}:|{Øiie}:|{iie}:|:e] _ ;
        ! *talo<t>a *risti<t>ä valo<>a valoj<>a *arvelu<t>a
        ! lovi<>a hevosi<>a, (autio<>a) herttua<>a 
{Øt}:t /<= BEGIN Co* Vo+ [Co|CØ]+ [{aØ}:a|{ao}:a] _ ;
        ! *koira<t>a *kissa<t>a 
{Øt}:t /<= BEGIN Co* Vo+ [Co|CØ]+ [Vo|VØ] {ij}: _ ;
	! *arkkui<t>a arkku<>ja *vatei<t>a vatej<>a *käsi<t>ä käsi<>ä
        ! (kantaji<t>a) *valoi<t>a valoj<>a 
{Øt}:t /<= Co VØ ({ØnØ}:Ø|{nØØ}:Ø) {ij}: _ ;
        ! *hevosi<t>a hevosi<>a
        ! vapaa<t>a ainoo<t>a
        ! arvelui<t>a loh<t>a autio<t>a, hevos<t>a sisar<t>a,
        ! autio<t>a leveä<t>ä, kulkija<t>a seitsemä<t>ä

!!!:a /<= :a :Ø* _ :Ø* :a ; ! *vapaa<>a

{dt}:t <=> _ :t ;
        ! mai<d>en mai<t>ten

{Ødt}:Ø => _ a [:n | :t] ; ! muu<>an (muu<d>an) muu<t>aman

{Ødt}:d => _ CLOSED ; ! muu<d>an

{Ødt}:t /<= _ CLOSED ; ! muu<t>amassa *muu<t>atta *muu<t>an

{tØ}:Ø <=> _ CLOSED ;
        ! pat<t>i pat<>in 

{td}:d <=> _ CLOSED ;
        ! va<t>i va<d>in

{tds}:s | {tdØs}:s | {tls}:s | {trs}:s| {tns}:s <=> _ (VØ) [:i | END] ;
        ! kä<s>i kä<s>iä kak<s>i jäl<s>i kan<s>i kahdeksa<s>
{tds}:d | {tdØs}:d | {tls}:l | {trs}:r <=> _ :e Ci (VØ) [Ci|END] ;
        ! kä<d>en kä<d>essä kah<d>en jäl<l>en kan<n>essa hir<r>essä
{tdØs}:Ø <=> _ VØ :t ; ! kah<>ta

{tns}:s <=> _ (VØ) :i ;
{tns}:n => _ CLOSED ;
{tns}:t /<= _ CLOSED ; 
        ! kan<s>i kan<s>ia kan<n>en kan<t>ta kan<t>ten kan<s>ia

{tntts}:s <=> _ VØ :i ;
{tntts}:t => _ OPEN , _ VØ [{Øt}:|END] ;
{tntts}:n => _ CLOSED , _ VØ t ; ! tunan<n>ne tuha<n>ten
        ! tuhan<s>ia tuhan<n>en tuha<n>ten tuha<t> tuha<t>ta tuhan<t>ena

{snttns}:s <=> _ VØ [{ij}: | END] ;
{snttns}:n => _ [CLOSED | VØ {nrs}:] ;
{snttns}:t => _ OPEN , _ VØ {Øt}: ;
        ! kandeksan<s>ia kahdeksan<n>en (kahdeksan<n>na) kahdeksan<t>ena
        ! kahdeksan<n>nen (kahdeksan<n>na) kahdeksan<n>en

{ttØ}:Ø <=> _ s VØ [{Øt}:|:t] ;
        ! vei<t>si vei<>stä vei<t>senä vei<t>siä

{uØ}:Ø <=> _ (o) {ij}: ;
        ! s<u>o~s<>oita, tenkkapo<o> tenkkapo<>ita

{ue}:u <=> _ [:t|:n] ;
        ! kuoll<u>t kuoll<u>tta kuoll<u>nna

{ØuØu}:Ø <=> _ [:t | {ij}: | END] ;
        ! kiiru<> kiiru<u>n kiirut<>ta

! {yØ}:Ø <=> _ (ö) {ij}: ; ! <y>ö~<>öitä

{äØö}:Ø|{äØö}:ö <=> _ {ij}: ;
        ! leve<ä> leve<>itä leve<ö>iden

!!!!t e n END => [h:|:l|:n|:r|:s|:t] :Ø* VØ :Ø* _ ; ! 

t e n /<= Vo :Ø* _ END ;
        ! *arvelu<ten> risti<ten> *ma<ten> *tie<ten>

{ij}: {dt}: {ØtIDEN}: /<= [Co - :h] :Ø* _ ;
        ! *käs<iden> koir<iden>

{ij}: {dt}: {ØtIDEN}: /<= BEGIN Co* Vo+ [Co|CØ]+ Vo _ ;
        ! *valo<iden> kisso<iden>

s e {eØ}: n => [VV | VhV] _ END ;
        ! ainoo<seen> vapaa<seen> korkee<seen>
	! (vapaha<sen>) (koiraha<sen>)

{ij}:i s i {iØ}: n => [Vo (:h|:Ø) (:Ø) VØ] _ ;
        ! vapa<isiin> aino<isiin> (ainoh<isin>)
	! saappa<isiin> (saappah<isin>) koira<isiin> (koirah<isin>

{ij}: e n /<= VVØ _ ;
        ! valo<jen> *tie<jen> *maa<jen> käs<ien> arvelu<jen>

{ij}: e n /<= Vo :Ø* VØ _ ;
        ! valo<jen> käs<ien> *te<jen> ma<jen> 

i n /<= [VV] _ END ;
        ! (valo<in>) (autio<in>) (vate<in>)
	! * maa<in> *tie<in> *suo<in>
